****************************************************************************************;
*     There are 3 programs to analyze the salmon and shellfish permit databases:       *;
*         1. permits.sas                                                               *;
*         2. harvest.sas                                                               *;
*         3. estimates.sas                                                             *;
*     This program reads and modifies data for permits                                 *;
*     This program is stored as O:\DSF\RTS\common\PAT\PERMITS\salmon\2013\permit.sas   *;
****************************************************************************************;

%LET PROJECT = SALMON;
%LET DATA = cislpm16c;
%LET YEAR = 2016;
%LET PROGRAM = PERMIT;


OPTIONS PAGENO=1;
OPTIONS NODATE;
OPTIONS SYMBOLGEN;
LIBNAME SASDATA BASE "O:\DSF\RTS\common\PAT\PERMITS\&PROJECT\&YEAR";
RUN;

TITLE1 "&PROJECT - &YEAR";

**************************************************************************;
*                              PERMIT DATA                               *;
**************************************************************************;

DATA SASDATA.&DATA; SET SASDATA.&DATA;
     CITY = UPCASE(CITY);


PROC PRINT DATA = SASDATA.&DATA (OBS=10);
     TITLE2 'PERMIT DATABASE';
     TITLE3 'FIRST 10 RECORDS';
RUN; 

PROC CONTENTS DATA = SASDATA.&DATA;
     TITLE3 '';
RUN;


DATA ISSUED; SET SASDATA.&DATA;
     RENAME STATUS = S;

PROC FREQ DATA = ISSUED;
     TABLES MAILING;
RUN;


DATA ISSUED; SET ISSUED;
     IF S = 'U' THEN STATUS = 'BLANK REPORT    ';
     IF S = 'N' THEN STATUS = 'DID NOT FISH';
     IF S = 'H' THEN STATUS = 'HARVEST REPORTED';
     IF S = 'Z' THEN STATUS = 'NON RESPONDENT';

     IF CITY = 'ANCHORGE' THEN CITY = 'ANCHORAGE';
     IF CITY = 'DENALI NATIONAL PARK' OR CITY = 'DENALI PARKS' THEN CITY = 'DENALI PARK';
     IF CITY = 'EGALE RIVER' THEN CITY = 'EAGLE RIVER';
     IF SUBSTR(CITY,1,6) = 'ELMEND' THEN CITY = 'ELMENDORF AFB';
     IF SUBSTR(CITY,1,6) = 'FORT R' THEN CITY = 'FORT RICHARDSON';

     IF OFFICE = 'NULL' THEN OFFICE = '';
     PERMIT = PERMITNO;
     IF OFFICE = '' THEN VENDORCOPY = 'N';
        ELSE VENDORCOPY = 'Y';
     IF S = 'Z' THEN RESPONDED = 'N'; 
        ELSE RESPONDED = 'Y';
     ALLOWED = ((FAMILYSI - 1) * 10) + 25;
     DROP IMAGEPATH PMRECID PHONE KEYDATE COMMENTS YEAR DATEISS PERMITNO S KEYID INITIAL LICENSE_NO;
RUN;

PROC SORT DATA = ISSUED; BY PERMIT;
     TITLE2 'SAS WORK ISSUED DATABASE';
     TITLE3 'CHECKS';
RUN;


PROC FREQ DATA = ISSUED;
     TABLES FAMILYSI OFFICE MAILING STATUS DUPPREF VENDORCOPY RESPONDED ALLOWED CITY STATE / NOPERCENT NOROW NOCOL;  *ALLOWED NOT POTS;
PROC FREQ DATA = ISSUED;
     TABLES STATUS * RESPONDED / OUT = SUMMARY_RESPONSE;
PROC FREQ DATA = ISSUED;
     TABLES MAILING / NOPRINT OUT = MAILING;
PROC FREQ DATA = ISSUED;
     TABLES OFFICE VENDORCOPY OFFICE*VENDORCOPY / NOPRINT OUT = OFFICE_BY_VENDOR;
PROC FREQ DATA = ISSUED;
     TABLES RESPONSE_METHOD RESPONSE_METHOD*OFFICE RESPONSE_METHOD*VENDORCOPY / NOPRINT OUT = RESPONSE_BY_VENDOR;
RUN;

PROC PRINT DATA = OFFICE_BY_VENDOR;
PROC PRINT DATA = RESPONSE_BY_VENDOR;


PROC PRINT DATA = ISSUED;
     WHERE ALLOWED LT 25 OR ALLOWED = .;
     VAR PERMIT FAMILYSI ALLOWED HHMEMBERS OFFICE MAILING STATUS;
     TITLE2 'ALLOWED PROBLEMS';
     TITLE3 'SAS WORK ISSUED DATABASE';
RUN;


PROC PRINT DATA = ISSUED;
     WHERE FAMILYSI GT 12;
     VAR PERMIT FAMILYSI OFFICE FIRST_NAME LAST_NAME CITY MAILING STATUS;
     TITLE2 'HUGE FAMILIES';
     TITLE3 'SAS WORK ISSUED DATABASE';
RUN;

PROC PRINT DATA = ISSUED;
     WHERE OFFICE = '';
     VAR PERMIT OFFICE FIRST_NAME LAST_NAME CITY MAILING STATUS;
     TITLE2 'NO VENDOR COPY';
     TITLE3 'SAS WORK ISSUED DATABASE';
RUN;

DATA NOVENDOR; SET ISSUED;
     IF OFFICE = ' ';
RUN;



PROC FREQ DATA = ISSUED;
     TABLES PERMIT / NOPRINT OUT = CHECK;

PROC PRINT DATA = CHECK;
     WHERE COUNT GT 1;
     TITLE2 'MULTIPLE PERMIT RECORDS';
     TITLE3 'SAS WORK ISSUED DATABASE';
RUN;

DATA ISSUED1; SET ISSUED (DROP = DUPPREF);RUN;

DATA PERSONAL; SET ISSUED;
     KEEP PERMIT ADLNO CITY STATE FIRST_NAME LAST_NAME;

DATA ISSUED; SET ISSUED;
     DROP DUPPREF ADLNO CITY STATE FIRST_NAME LAST_NAME HHMEMBERS;
PROC PRINT DATA = ISSUED (OBS = 10);
     TITLE2 'PERMANENT SAS ISSUED DATABASE';
     TITLE3 'FIRST 10 RECORDS';
RUN;


DATA SASDATA.ISSUED; SET ISSUED;
DATA SASDATA.PERSONAL; SET PERSONAL;

RUN;

DATA ISSUED; SET SASDATA.ISSUED;
     
PROC MEANS SUM DATA=ISSUED NOPRINT;
     CLASS VENDORCOPY RESPONDED MAILING;
     VAR PERMIT;
     OUTPUT OUT = SUMMARY;
RUN;

PROC SORT; BY VENDORCOPY RESPONDED MAILING _TYPE_ _FREQ_;
DATA SUMMARY; SET SUMMARY; BY VENDORCOPY RESPONDED MAILING _TYPE_ _FREQ_;
     IF FIRST._FREQ_;
     DROP PERMIT _STAT_ _TYPE_;

PROC SORT; BY MAILING RESPONDED VENDORCOPY;   
PROC PRINT DATA = SUMMARY;
     TITLE2 'SUMMARY OF DATA';
     TITLE3 'USED IN ESTIMATING THE NUMBER OF PERMITS ISSUED';
RUN;


DATA TABLE; SET SUMMARY;
     IF VENDORCOPY = 'Y' AND RESPONDED = ' ' AND MAILING = . THEN VYRB = _FREQ_;
     IF VENDORCOPY = 'N' AND RESPONDED = 'Y' AND MAILING = . THEN VNRY = _FREQ_;
     IF VENDORCOPY = 'Y' AND RESPONDED = 'N' AND MAILING = . THEN VYRN = _FREQ_;
     IF VENDORCOPY = 'Y' AND RESPONDED = 'Y' AND MAILING = 0 THEN NUMERATOR = _FREQ_;   
     IF VENDORCOPY = 'Y' AND RESPONDED = ' ' AND MAILING = . THEN DENOMINATOR = _FREQ_;
     IF VYRB = . AND VNRY = . AND VYRN = . AND NUMERATOR = . AND DENOMINATOR = . THEN DELETE;
     KEEP VYRB VNRY VYRN NUMERATOR DENOMINATOR;


PROC MEANS SUM NOPRINT;
     VAR VYRB VNRY VYRN NUMERATOR DENOMINATOR;
     OUTPUT OUT = TABLE SUM=; 

DATA TABLE; SET TABLE (DROP = _TYPE_ _FREQ_);
     VOL_RESP_RATE = NUMERATOR/DENOMINATOR;
     VYRY = VYRB - VYRN;
     VNRN = ROUND((VNRY / VOL_RESP_RATE),1);
     VNRB = ROUND((VNRY + VNRN),1);
     VBRY = VYRY + VNRY;
     VBRN = VYRN + VNRN;

     VBRB = VYRY + VNRY + VYRN + VNRN;
     VBRB1 = VBRY + VBRN;
     VBRB2 = VYRB + VNRB;

     VAR_VOL_RESP_RATE = (VOL_RESP_RATE * (1-VOL_RESP_RATE))/(VYRB-1);
     VAR_VNRB = VNRY**2 * (1/(VOL_RESP_RATE**4))*VAR_VOL_RESP_RATE;
     VAR_VBRB = VAR_VNRB;

     DROP NUMERATOR DENOMINATOR;
PROC PRINT;
     TITLE2 'ESTIMATES OF THE NUMBER OF PERMITS ISSUED';
     TITLE3;
RUN;



DATA TOTAL_ISSUED; SET TABLE (KEEP = VBRB  VAR_VBRB);
     RENAME VBRB = NHAT VAR_VBRB = VAR_NHAT;
PROC PRINT;
RUN;

DATA SASDATA.TOTAL_ISSUED; SET TOTAL_ISSUED;
RUN;



DATA COMMENTS; SET SASDATA.&DATA;
     WHERE COMMENTS NE '';
     KEEP PERMITNO COMMENTS;

PROC EXPORT DATA= WORK.COMMENTS 
            OUTFILE= "O:\DSF\RTS\common\PAT\PERMITS\&PROJECT\&YEAR\PU PERMITS &YEAR" 
            DBMS=EXCEL2000 REPLACE;
            SHEET="PERMIT COMMENTS"; 
RUN;

PROC EXPORT DATA= WORK.MAILING
            OUTFILE= "O:\DSF\RTS\common\PAT\PERMITS\&PROJECT\&YEAR\PU PERMITS &YEAR" 
            DBMS=EXCEL2000 REPLACE;
            SHEET="MAILING SUMMARY";
RUN;

PROC EXPORT DATA= WORK.SUMMARY_RESPONSE
            OUTFILE= "O:\DSF\RTS\common\PAT\PERMITS\&PROJECT\&YEAR\PU PERMITS &YEAR" 
            DBMS=EXCEL2000 REPLACE;
            SHEET="SUMMARY_RESPONSE"; 
RUN;

PROC EXPORT DATA= WORK.NOVENDOR 
            OUTFILE= "O:\DSF\RTS\common\PAT\PERMITS\&PROJECT\&YEAR\PU PERMITS &YEAR" 
            DBMS=EXCEL2000 REPLACE;
            SHEET="NOVENDOR"; 
RUN;

PROC EXPORT DATA= WORK.ISSUED1
            OUTFILE= "O:\DSF\RTS\common\PAT\PERMITS\&PROJECT\&YEAR\PU PERMITS &YEAR" 
            DBMS=EXCEL2000 REPLACE;
            SHEET="PERMIT RECORDS"; 
RUN;

PROC EXPORT DATA= WORK.TOTAL_ISSUED 
            OUTFILE= "O:\DSF\RTS\common\PAT\PERMITS\&PROJECT\&YEAR\PU PERMITS &YEAR" 
            DBMS=EXCEL2000 REPLACE;
            SHEET="TOTAL ISSUED"; 
RUN;
